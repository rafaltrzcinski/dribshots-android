apply plugin: 'com.android.application'


rootProject.appModulePlugins.each {
    apply plugin: it
}

buildscript {
    dependencies {
        classpath rootProject.kotlinPlugin
    }
}


android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId "com.rafaltrzcinski.dribshots"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    rootProject.appModuleDependencies.each {
        add(it.configuration, it.dependency, it.options)
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.google.com' }
}

// Groovy plugins is not working with DataBinding class generator.
// Groovy is used only for tests so there is no harm to just disable some tasks from build chain.
afterEvaluate {
    tasks.findAll { it.name.endsWith("Groovyc") }.findAll { !it.name.contains("Unit") }.each {
        println "Disabling " + it
        it.enabled = false
    }
}
